import proguard.gradle.ProGuardTask

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'jacoco'

String genDir = "$projectDir/src-gen"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir genDir
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile group: 'us.monoid.web', name: 'resty', version: '0.3.2'
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.4.0.201906121030-r'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9.3'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.9'
    compile 'com.github.Frotty:SwingDarkFlatTable:a033300526'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'io.github.microutils:kotlin-logging:1.6.26'
    compile 'com.github.Frotty:SimpleRegistry:f96dda96bd'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile 'org.testng:testng:6.14.3'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

configurations.all {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
}

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        flatDir dirs: 'src/main/resources/'
        mavenCentral()
    }
    dependencies {
        classpath ':proguard'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

version '1.3.4.1'

task versionInfoFile {
    description "Generates a file CompileTimeInfo.java with version number etc."

    doLast {
        def dir = new File("$projectDir/src-gen/file/")
        dir.mkdirs()
        def f = new File(dir, 'CompileTimeInfo.kt')

        String gitRevision = "unknown-version"
        String gitRevisionlong = "unknown-version"

        new ByteArrayOutputStream().withStream { os ->
            exec {
                executable = 'git'
                args = ['describe', '--tags', '--always']
                standardOutput = os
            }
            gitRevision = os.toString().trim()
        }

        new ByteArrayOutputStream().withStream { os ->
            exec {
                executable = 'git'
                args = ['describe', '--tags', '--always', '--abbrev=0']
                standardOutput = os
            }
            gitRevisionlong = os.toString().trim()
        }

        String setupVersion = "${version}-${gitRevision}"


        String currentTime = new Date().format("yyyy/MM/dd KK:mm:ss")

        f.text = """
package file

object CompileTimeInfo {
\tval time="${currentTime}"
\tval revision="${gitRevision}"
\tval revisionLong="${gitRevisionlong}"
\tval version="${setupVersion}"
}"""

    }
}

compileKotlin.dependsOn versionInfoFile

task dist(type: Jar) {
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA','proguard.jar'
    manifest {
        attributes 'Implementation-Title': 'Wurst Setup',
                'Implementation-Version': version,
                'Main-Class': 'file.SetupMain'
    }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
dist.dependsOn classes
dist.dependsOn versionInfoFile

dist.archiveName = "${jar.baseName}.${jar.extension}"

task proguardDist(type: ProGuardTask) {
    configuration 'proguard-project.txt'
    injars 'build/libs/WurstSetup.raw.jar'
    outjars 'build/libs/WurstSetup.jar'

}
proguardDist.dependsOn dist

task copy_jar(type: Copy) {
    mkdir("downloads/")
    from 'build/libs/'
    into 'downloads/'
}

copy_jar.dependsOn(dist)

mainClassName = "file.SetupMain"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


